{ lib, pkgs, ... }:

let
  photogimp-src = pkgs.fetchFromGitHub {
    owner = "Diolinux";
    repo = "PhotoGIMP";
    rev = "a1565875453babfdb98256c43e633c4a03913599";
    sha256 = "E38fFiIHqJsvt03F+Wds7abyalMcygrJ8KYNW5hUBh8=";
  };
in self: super: {

  # This derivation overrides the gimp user configuration, which prohibits the
  # user from creating a custom gimp configuration. They are forced to use the
  # default photogimp one

  # The user is still able to use a custom gimp configuration if they override
  # the GIMP2_DIRECTORY themselves

  photogimp_gimpPatched = let

    desktop-patch = pkgs.writeTextFile {
      name = "desktop.patch";
      text = ''
 desktop/gimp.desktop.in.in | 8 ++++----
 1 file changed, 4 insertions(+), 4 deletions(-)

diff --git a/desktop/gimp.desktop.in.in b/desktop/gimp.desktop.in.in
index c0a98e9..998e274 100644
--- a/desktop/gimp.desktop.in.in
+++ b/desktop/gimp.desktop.in.in
@@ -1,14 +1,14 @@
 [Desktop Entry]
-Version=1.0
+Version=1.1
 Type=Application
-_Name=GNU Image Manipulation Program
+_Name=PhotoGIMP
 _GenericName=Image Editor
 _Comment=Create images and edit photographs
 # Translators: Search terms to find this application. Do NOT translate or localize the semicolons! The list MUST also end with a semicolon!
-_Keywords=GIMP;graphic;design;illustration;painting;
+_Keywords=GIMP;PhotoGIMP;graphic;design;illustration;painting;
 Exec=@GIMP_COMMAND@ %U
 TryExec=gimp-@GIMP_APP_VERSION@
-Icon=gimp
+Icon=photogimp
 Terminal=false
 Categories=Graphics;2DGraphics;RasterGraphics;GTK;
 StartupNotify=true
      '';
    };

  in super.gimp.overrideAttrs (old: {
    name = "photogimp_gimpPatched";

    # patch to use photogimp .desktop file
    patches = (old.patches or []) ++ [
      desktop-patch
    ];

  });

  photogimp_config = self.stdenv.mkDerivation rec {
    name = "photogimp_config";
    dontBuild = true;
    installPhase = ''
      mkdir -p $out
      cp -aR $src/.var/app/org.gimp.GIMP/config/GIMP/2.10/* $out
      # themerc is written for flatpak, not native install
      rm $out/themerc
      echo "# GIMP themerc
#
# This file is written on GIMP startup and on every theme change.
# It is NOT supposed to be edited manually. Edit your personal
# gtkrc file instead.

style \"gimp-spin-scale-style\"
{
  GimpSpinScale::compact = 1
}

class \"GimpSpinScale\" style \"gimp-spin-scale-style\"

include \"${pkgs.photogimp_gimpPatched}/share/gimp/2.0/themes/Dark/gtkrc\"
include \"${pkgs.photogimp_gimpPatched}/etc/gimp/2.0/gtkrc\"
include \"$out/gtkrc\"

# end of themerc" > $out/themerc
      chmod 555 $out/themerc
    '';
    src = photogimp-src;
  };

  photogimp_icon = self.stdenv.mkDerivation rec {
    name = "photogimp_icon";
    dontBuild = true;
    installPhase = ''
      mkdir -p $out/share/icons
      cp -aR $src/.local/share/icons $out/share
    '';
    src = photogimp-src;
  };

}
